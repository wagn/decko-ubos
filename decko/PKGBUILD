developer=http://grasscommons.org/
url=http://wagn.org/
maintainer=${developer}
pkgname=decko
pkgver=1.18.6
pkgrel=1
pkgdesc="Wiki platform for collaborating to build custom web systems"
arch=('any')
license=("GPL")
source=("https://github.com/wagn/wagn/archive/v${pkgver}.zip")
depends=(
    'nodejs'
    'git'
    'smtp-server' )
options=('!strip')
sha512sums=('45e4f8682a5e88ef45d658844c1c051468b89204a9e297d967fbde41cee2befb9cd57f69dd3c282820509050af5e5fff7a7fc83205d89ed93e60feaf8c08b596')

build() {
    [[ -d .bundle ]] || mkdir .bundle
    cp ${startdir}/web/Gemfile . # need to copy, so Gemfile.lock is here, not in git
    bundle install --path $(pwd)/.bundle
}

package() {
# Manifest
    mkdir -p ${pkgdir}/var/lib/ubos/manifests
    install -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/var/lib/ubos/manifests/${pkgname}.json

# Icons
    mkdir -p ${pkgdir}/srv/http/_appicons/$pkgname
    install -m644 ${startdir}/appicons/{72x72,144x144}.png ${pkgdir}/srv/http/_appicons/$pkgname/

# Data directory
    mkdir -p ${pkgdir}/var/lib/${pkgname}

# Templates
    mkdir -p ${pkgdir}/usr/share/${pkgname}/tmpl
    cp ${startdir}/tmpl/* ${pkgdir}/usr/share/${pkgname}/tmpl/

# Initial SQL
    mkdir -p ${pkgdir}/usr/share/${pkgname}/sql
    cp ${startdir}/sql/bootstrap.sql ${pkgdir}/usr/share/${pkgname}/sql/
    
# Code
    mkdir -p ${pkgdir}/usr/share/${pkgname}/web
    cp -r -p ${startdir}/web/* ${pkgdir}/usr/share/${pkgname}/web/
    mkdir -p ${pkgdir}/usr/share/${pkgname}/bin
    install -m755 ${startdir}/bin/* ${pkgdir}/usr/share/${pkgname}/bin/

# Logs
    # Should move to systemd journal some day
    mkdir -p ${pkgdir}/var/log/${pkgname}
    chown http:http ${pkgdir}/var/log/${pkgname}

# Gems
    mkdir -p ${pkgdir}/usr/share/${pkgname}/wagn-gem
    cp -r -p ${srcdir}/wagn-${pkgver}/* ${pkgdir}/usr/share/${pkgname}/wagn-gem/
    mkdir -p ${pkgdir}/usr/share/${pkgname}/{bin,gems}
    cp -r -p ${srcdir}/.bundle ${pkgdir}/usr/share/${pkgname}/.bundle
}

